<h1>View.Update</h1>

<h2>Syntax</h2>

<p><strong>View.Update</strong></p>

<h2>Description</h2>

<p>The <strong>View.Update</strong> procedure updates a Run window from an offscreen bitmap. It is used with the command <strong>View.Set</strong> ("offscreenonly") which prevents the Run window from being updated until the <strong>View.Update</strong> command is given.</p>

<h2>Example</h2>

<p>This program displays 30 circles bouncing around the screen. To seethe effect without <strong>View.Update</strong>, comment out the line<strong>View.Set</strong> ("offscreenonly")</p>

<pre><code>    % Place some circles around the screen
    const RADIUS : int := 30
    const NUM_BALLS : int := 20
    var x, y, dx, dy, clr : array 1 .. NUM_BALLS of int
    for i : 1 .. NUM_BALLS
        x (i) := Rand.Int (RADIUS, maxx - RADIUS)
        y (i) := Rand.Int (RADIUS, maxy - RADIUS)
        dx (i) := Rand.Int (-3, 3)
        dy (i) := Rand.Int (-3, 3)
        clr (i) := Rand.Int (1, 15)
    end for

    % Now, any drawing to the screen won't appear until a 
    % View.Update is given.
    View.Set ("offscreenonly")
    loop
        cls % Clear the offscreen window
        for i : 1 .. NUM_BALLS
            if x (i) + dx (i) &lt; RADIUS or 
                    x (i) + dx (i) &gt; maxx - RADIUS then
                dx (i) := -dx (i)
            end if
            if y (i) + dy (i) &lt; RADIUS or 
                    y (i) + dy (i) &gt; maxy - RADIUS then
                dy (i) := -dy (i)
            end if
            x (i) := x (i) + dx (i)
            y (i) := y (i) + dy (i)
            Draw.FillOval (x (i), y (i), RADIUS, RADIUS, clr (i))
        end for
        % All the circles have been drawn.  Now update the screen.
        View.Update
        Time.Delay (5)
    end loop
</code></pre>

<h2>Details</h2>

<p>All Turing Run windows have both an onscreen visible window and an offscreen window. Whenever any output is sent to the screen, both the onscreen window and the offscreen window are updated. When the Run window needs to be updated (for example when another window is moved over top of it and then removed), Turing copies the offscreen window onto the onscreen window.</p>

<p>When the <strong>View.Set</strong> ("offscreenonly") command is given, Turing no longer draws to the onscreen window when any drawing command is given. However, it does update the offscreen window. When the <strong>View.Update</strong> command is given, the entire offscreen window is copied to the onscreen window.</p>

<p>This can be used to provide smooth, flicker-free animation. Animated objects flicker when the object being animated disappears from the onscreen window for a period of time. By using <strong>View.Set</strong> ("offscreenonly") / <strong>View.Update</strong>, the onscreen window is never blank. Instead, the offscreen window drawn over top off the on screen window, replacing it. This means that the on-screenwindow is never blanked out, eliminating the flickering found in the animation.</p>

<h2>Details</h2>

<p>It's very easy to forget that no output will appear in the Run window when using <strong>View.Update</strong>. Remember to use <strong>View.Set</strong>("nooffscreenonly") to turn off this feature sending output for debugging purposes.</p>

<h2>Details</h2>

<p><strong>View.Update</strong> should not be used in conjunction with the <strong>Sprite</strong> module. Sprites can be considered a limited version of this technique. <strong>View.Update</strong> also works well when the entire background is changing.</p>

<h2>Status</h2>

<p>Exported qualified.</p>

<p>This means that you can only call the function by calling <strong>View.Update</strong>, not by calling <strong>Update</strong>.</p>

<h2>See also</h2>

<p><strong><a href="view_set.html">View.Set</a></strong> for the "offscreenonly" and "nooffscreenonly" options.</p>

<p><strong><a href="view_updatearea.html">View.UpdateArea</a></strong> for updatingpart of the window at a time.</p>
