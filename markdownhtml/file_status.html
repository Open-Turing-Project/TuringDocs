<h1>File.Status</h1>

<h2>Syntax</h2>

<p><strong>File.Status</strong> (<em>pathName</em> : <strong>string</strong>, <strong>var</strong> <em>size</em>, <em>attribute</em>, <em>fileTime</em> : <strong>int</strong>)</p>

<h2>Description</h2>

<p><strong>File.Status</strong> is used to get assorted information about a file or directory. When the function is called with a specified <em>pathName</em>, it returns the information about the file in the other parameters.
The <em>size</em> parameter is the size of the file in bytes.
The <em>attribute</em> parameter has its individual bits set as exactly as the <em>attribute</em> parameter in <strong>Dir.GetLong</strong> subprogram does. See <strong>Dir.GetLong</strong> for the list of attribute constants.
The <em>fileTime</em> is the time of last modification of the file. It is returned as the number of seconds since 00:00:00 GMT 1/1/1970. To convert this to a string, use <strong>Time.SecDate.</strong></p>

<h2>Details</h2>

<p>If the <strong>File.Status</strong> call fails, <em>size</em>, <em>attribute</em> and <em>fileTime</em> are all set to 1. <strong>Error.Last</strong> will return a non-zero value indicating the reason for the failure. <strong>Error.LastMsg</strong> will return a string which contains the textual version of the error.</p>

<h2>Example</h2>

<p>This program prints information about the NotePad.exe application in Windows foundat "<em>c:\windows\notepad.exe</em>". (Note on some systems, this will be "c:\winnt\notepade.exe"</p>

<pre><code>    const pathName : string := "c:/windows/notepad.exe"
    var size, attribute, fileTime : int
    File.Status (pathName, size, attribute, fileTime )
    if Error.Last = eNoError then
        put "      Name: ", File.FullPath (pathName)
        put "   Created: ", Time.SecDate (fileTime)
        put "      Size: ", size, " bytes"
        put "Attributes: " ..
        if (attribute and ootAttrDir) not= 0 then
            put "Directory " ..
        else
            put "" ..
        end if
        if (attribute and ootAttrRead) not= 0 then
            put "Readable " ..
        else
            put "" ..
        end if
        if (attribute and ootAttrWrite) not= 0 then
            put "Writable " ..
        else
            put "" ..
        end if
        if (attribute and ootAttrExecute) not= 0 then
            put "Executable", skip
        else
            put skip
        end if
    else
        put "Unable to get file information"
        put "Error: ", Error.LastMsg, skip
    end if
</code></pre>

<h2>Execute</h2>

<p>Another example program is available that uses <strong>File.Status</strong> to determine the status of several files and directories.</p>

<h2>Status</h2>

<p>Exported qualified.
This means that you can only call the function by calling <strong>File.Status</strong>, not by calling <strong>Status</strong>.</p>
