<h1>array</h1>

<h2>Syntax</h2>

<p><strong>array</strong> <em>indexType</em> { , <em>indexType</em> } <strong>of</strong> <em>typeSpec</em></p>

<h2>Description</h2>

<p>An array consists of a number of elements. The <em>typeSpec</em> gives the type of these elements. There is one element for each item in the (combinations of) range(s) of the <em>indexType(s)</em>. In the following example, the array called <em>marks</em> consists of 100 elements, each of which is an integer.</p>

<h2>Example</h2>

<pre><code>    var marks : array 1 .. 100 of int
    &#133;
    var sum : int := 0
    for i : 1 .. 100        % Add up the elements of marks
        sum := sum + marks (i)
    end for
</code></pre>

<h2>Details</h2>

<p>In the above example, <em>marks(i)</em> is the <em>i</em>-th element of the <em>marks</em> array. We call <em>i</em> the <em>index</em> or <em>subscript</em> of <em>marks</em>. In Turing, a subscript is surrounded by parentheses, not by square brackets as is the case in the Pascal or C-like languages.</p>

<h2>Example</h2>

<p>The <em>prices</em> array shows how an array can have more than one dimension. This array has one dimension for the year (1988, 1989 or 1990) and another for the month (1 .. 12). There are 36 elements of the array, one for each month of each year.</p>

<pre><code>    var price : array 1988 .. 1990, 1 .. 12 of int
    &#133;
    var sum : int := 0
    for year : 1988 .. 1990     % For each year
        for month : 1 .. 12     % For each month
            sum := sum + price (year, month)
        end for
    end for
</code></pre>

<h2>Details</h2>

<p>Each <em>indexType</em> must contain at least one item. The range 1 .. 0, for example, would not be allowed. Each index type must be a subrange of the integers, characters (the <strong>char</strong> type), or of an enumerated type, an (entire) enumerated type, the <strong>char</strong> type, the <strong>boolean</strong> type, or a named type which is one of these.</p>

<p>Arrays can also be declared in the form</p>

<p>The upper bound of <em>a</em> will be computed from the count of the initializing values. Both <strong>var</strong> and <strong>const</strong> arrays can be declared this way. An array variable/constant declared with "*" as an upper bound must have an initializing list. Only one dimensional arrays may be declared in this form.</p>

<p>Arrays can be assigned as a whole (to arrays of an equivalent type), but they cannot be compared.</p>

<p>An array can be initialized in its declaration using <strong>init</strong>. For details, see <strong>var</strong> and <strong>const</strong> declarations.</p>

<pre><code>    var a : array 1 .. * of typeSpec := init (&#133;)
</code></pre>

<h2>Example</h2>

<p>In this example, the size of the array is not known until run time.</p>

<pre><code>    var howMany : int
    get howMany
    var height : array 1 .. howMany of real
        &#133;read in all the elements of this array&#133;
    function total (a : array 1 .. * of real) : real
        var sum : real := 0
        for i : 1 .. upper (a)
            sum := sum + a (i)
        end for
        result sum
    end total

    put "Sum of the heights is ", total (height)
</code></pre>

<h2>Details</h2>

<p>The ends of the range of a subscript are called the <em>bounds</em> of the array. If these values are not known until run time, the array is said to be <em>dynamic</em>. In the above example, <em>height</em> is a dynamic array. Dynamic arrays can be declared as variables, as in the case for <em>height</em>. However, dynamic arrays cannot appear inside other types such as records, and cannot be named types. Dynamic arrays cannot be assigned and cannot be initialized using <strong>init</strong>.</p>

<p>In the above example, <strong>upper</strong><em>(a)</em> returns the size of <em>a</em>. See also <strong>upper</strong> and <strong>lower</strong>.</p>

<p>In the declaration of an array parameter, the upper bound can be given as an asterisk (<strong>*</strong>), as is done in the above example. This means that the upper bound is taken from that of the corresponding actual parameter (from <em>height</em> in this example).</p>

<p>You can have arrays of other types, for example arrays of record. If <em>R</em> is an array of records, then <em>R(i).f</em> is the way to access the <em>f</em> field of the <em>i</em>-th element of array <em>R</em>.</p>

<h2>Details</h2>

<p>Arrays can also be made resizeable. This is done using the <strong>flexible</strong> keyword. The declaration syntax is:</p>

<p>The indices may have compile-time or run-time upper bounds (the lower bound must be compile-time). The upper bounds can be changed by using:</p>

<p>The existing array entries will retain their values, except that any index made smaller will have the corresponding array entries lost. Any index made larger will have the new array entries uninitialized (if applicable).</p>

<p>Additionally, the upper bound (both in the declaration and the <strong>new</strong> statement) can be made one less than the lower bound. This effectively makes an array that contains 0 elements. It can later be increased in size with another <strong>new</strong>.</p>

<p>In the current implementation (2002), with a multi-dimensional array with a non-zero number of total elements, it is a run-time error to change any but the first dimension (unless one of the new upper bounds is one less than the corresponding lower bound, giving 0 elements in the array) as the algorithm to rearrange the element memory locations has not yet been implemented.</p>

<p>Currently, only variables can be declared in this form. There is no flexible array parameter type, although a flexible array can be passed to an array parameter with &#147;<strong>*</strong>&#148; as the upper bound.</p>

<pre><code>    var name : flexible array indexType { , indexType } of typeSpec        new name , newUpper1 {,newUpper2}
</code></pre>

<h2>Example</h2>

<p>In this example, the array is resized to fit the number of elements in the file.</p>

<pre><code>    function getLines (fileName : string) : int
        var f, numLines : int
        var line : string
        open : f, fileName, get
        numLines := 0
        loop
            exit when eof (f)
            get : f, line : *
            numLines += 1
        end loop
        close : f
        result numLines
    end getLines 

    procedure readFile (var lines : array 1 .. * of string, fileName : string)
        var f : int
        var line : string
        open : f, fileName, get
        for i : 1 .. upper (lines)
            get : f, lines (i) : *
        end for    
        close : f
    end readFile

    var lines : flexible array 1 .. 0 of string
    new lines, getLines ("text.dat")
    readFile (lines, "text.dat")
    for i : 1 .. upper (lines)
        put lines (i)
    end for
</code></pre>

<h2>See also</h2>

<p><strong><a href="init.html">init</a></strong> to initialize arrays, <strong><a href="flexible.html">flexible</a></strong> to declare resizable arrays and <em><a href="indextype.html">indexType</a></em> to specify the index of an array.</p>
